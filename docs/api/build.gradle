/*
 * Copyright 2002-2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// -----------------------------------------------------------------------------
// API documentation gradle build file.
//
// @author cbeams
// -----------------------------------------------------------------------------

apply plugin: 'base'

/**
 * Build aggregated JavaDoc HTML for all core project classes.  Result is
 * suitable for packaging into a distribution zip or viewing directly with
 * a browser.
 *
 * @author cbeams
 * @author ltaylor
 * @see http://gradle.org/0.9-preview-3/docs/javadoc/org/gradle/api/tasks/javadoc/Javadoc.html
 */
task build(type: Javadoc) {
    description = "Builds aggregated JavaDoc HTML for all core project classes."

    // this task is a bit ugly to configure. it was a user contribution, and
    // Hans tells me it's on the roadmap to redesign it.
    srcDir = file("${projectDir}/src")
    destinationDir = buildDir
    tmpDir = file("${buildDir}/tmp")
    optionsFile = file("${tmpDir}/apidocs/javadoc.options")
    options.stylesheetFile = file("${srcDir}/spring-javadoc.css")
    options.overview = "${srcDir}/overview.html"
    options.docFilesSubDirs = true
    title = "Spring AMQP ${version} API"

    // collect all the sources that will be included in the javadoc output
    source coreprojects.collect {project ->
        project.sourceSets.main.allJava
    }

    // collect all main classpaths to be able to resolve @see refs, etc.
    // this collection also determines the set of projects that this
    // task dependsOn, thus the runtimeClasspath is used to ensure all
    // projects are included, not just *dependencies* of all classes.
    // this is awkward and took me a while to figure out.
    classpath = files(coreprojects.collect {project ->
        project.sourceSets.main.runtimeClasspath
    })

    // copy the images from the doc-files dir over to the target
    doLast { task ->
        copy {
            from file("${task.srcDir}/doc-files")
            into file("${task.destinationDir}/doc-files")
        }
    }
}

// Define remoteSiteDir and sshHost in gradle.properties
def remoteDocsDir = null

if (hasProperty('remoteSiteDir')) {
    remoteDocsDir="$remoteSiteDir/docs/1.0.x"
}

configurations { scpAntTask }
dependencies {
    scpAntTask("org.apache.ant:ant-jsch:1.8.1")
}

/**
 * Upload API documentation via SSH (scp).
 *
 * TODO: remove duplication between this and :docs:reference:upload task
 *
 * @author cbeams
 * @author ltaylor
 */
task upload(type: Tar, dependsOn: build) {
    def docUrl = "http://${sshHost}/${project.rootProject.name}/docs/${project.version.wildcardValue}/api"
    description = "Uploads API documentation to ${docUrl}"
    classifier = 'api'
    remoteDir = remoteDocsDir
    compression = Compression.BZIP2

    into('api') {
        from build.destinationDir
    }

    doLast() {
        String username = project.property('sshUsername')
        String keyfile = project.property('sshPrivateKey')
        String host = project.property('sshHost')

        project.ant {

            // TODO: get progress metrics by hooking in a listener via Jsch, or ivy resolver, etc.
            taskdef(name: 'scp',
                classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.scpAntTask.asPath)
            taskdef(name: 'sshexec',
                classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec', classpath: configurations.scpAntTask.asPath)

            // make the remote dir if it doesn't exist yet
            sshexec(host: host, username: username, keyfile: keyfile, command: "mkdir -p $remoteDir")

            // copy the archive, unpack it, then delete it
            def fqRemoteDir = "${username}@${host}:${remoteDir}"
            def unpackCommand = "cd ${remoteDir} && tar -xjf ${archiveName}"
            def deleteCommand = "rm ${remoteDir}/${archiveName}"

            println "scp ${archivePath} -> ${fqRemoteDir}"
            scp(file: archivePath, todir: fqRemoteDir, keyfile: keyfile)
            println "sshexec ${unpackCommand}"
            sshexec(host: host, username: username, keyfile: keyfile, command: unpackCommand)
            println "sshexec ${deleteCommand}"
            sshexec(host: host, username: username, keyfile: keyfile, command: deleteCommand)
            println "UPLOAD SUCCESSFUL - validate by visiting ${docUrl}"
        }
    }
}
