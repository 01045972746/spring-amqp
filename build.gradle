// -----------------------------------------------------------------------------
// main gradle build file for Spring AMQP
// -----------------------------------------------------------------------------
import org.springframework.build.Version;

// -----------------------------------------------------------------------------
// configuration for all projects including this one (the root project)
// -----------------------------------------------------------------------------
allprojects {
    group = 'org.springframework.amqp'
    version = new Version('1.0.0.BUILD-SNAPSHOT')

    repositories {
        mavenRepo urls: "file://" + System.properties['user.home'] + "/.m2/repository"
        mavenCentral()
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/milestone'
        mavenRepo urls: 'http://maven.springframework.org/snapshot/'
        mavenRepo urls: 'http://maven.springframework.org/release' // necessary for aws.maven dependency
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/release'
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/external'
    }
}

// -----------------------------------------------------------------------------
// configuration for all subprojects
// see: settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
subprojects { }

javaprojects = subprojects.findAll {
    project -> project.path.startsWith(':spring-') && project != project.project(':spring-amqp-samples')
}

sampleprojects = project(':spring-amqp-samples').subprojects

coreprojects = javaprojects - sampleprojects

// -----------------------------------------------------------------------------
// configuration for all java subprojects
// all subprojects that start with spring-* are assumed to be java projects
// all subprojects that start with spring-amqp-samples are assumed to be samples
// -----------------------------------------------------------------------------
configure(javaprojects) {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    springVersion = '3.0.3.RELEASE'

    // dependencies that are common across all java projects
    dependencies {
        testCompile 'junit:junit:4.7'
    }

    processResources.dependsOn eclipse
}

// -----------------------------------------------------------------------------
// configuration for all core (non-sample) java subprojects
// all subprojects that start with spring-* are assumed to be java projects
// all subprojects that start with spring-amqp-samples are assumed to be samples
// -----------------------------------------------------------------------------
configure(coreprojects) {
    erlangLicense = false
    apply from: "$rootDir/gradle/bundlor.gradle"
    apply from: "$rootDir/gradle/maven-deployment.gradle"

    dependencies {
        compile ('log4j:log4j:1.2.15') {
            exclude group: 'javax.mail', name: 'mail'
            exclude group: 'javax.jms', name: 'jms'
            exclude group: 'com.sun.jdmk', name: 'jmxtools'
            exclude group: 'com.sun.jmx', name: 'jmxri'
        }
        compile 'org.slf4j:jcl-over-slf4j:1.5.10'
        compile 'org.slf4j:slf4j-api:1.5.10'
        runtime 'org.slf4j:slf4j-log4j12:1.5.10'
    }
}

configure(sampleprojects) {
    apply plugin: 'maven'
    group = "${group}.samples"

    dependencies {
        runtime 'log4j:log4j:1.2.14'
    }

    task writePom {
        generatedPomFileName = "pom.xml"

        // participate in incremental build: do not execute if no change in classpath
        //inputs.files(configurations.runtime) // includes configurations.compile

        // enable partial cleaning with `gradle cleanWritePom`
        // and incremental build - if it's been removed by someone it'll be regenerated
        outputs.files(generatedPomFileName)

        artifactId = null
        artifactName = null
        artifactDescription = null

        doLast() {
            p = pom {
                project {
                    artifactId = artifactId
                    name = artifactName
                    description = artifactDescription
                    properties {
                        setProperty('project.build.sourceEncoding', 'UTF8')
                    }
                    build {
                        plugins {
                            plugin {
                                groupId = 'org.apache.maven.plugins'
                                artifactId = 'maven-compiler-plugin'
                                configuration {
                                    source = '1.5'
                                    target = '1.5'
                                }
                            }
                            plugin {
                                groupId = 'org.apache.maven.plugins'
                                artifactId = 'maven-surefire-plugin'
                                configuration {
                                    includes {
                                        include = '**/*Tests.java'
                                        include = '**/Client.java'
                                        include = '**/Server.java'
                                    }
                                    excludes {
                                        exclude = '**/*Abstract*.java'
                                    }
                                }
                            }
                        }
                    }
                }
            }
            p.whenConfigured { pom -> pom.artifactId = artifactId }
            p.writeTo(generatedPomFileName)
        }

        processResources.dependsOn writePom
    }
}

// -----------------------------------------------------------------------------
// import tasks related to releasing and managing the project
// -----------------------------------------------------------------------------
apply from: "$rootDir/gradle/release.gradle"
apply from: "$rootDir/gradle/wrapper.gradle"

// -----------------------------------------------------------------------------
// configuration for each individual core java subproject
// -----------------------------------------------------------------------------
project('spring-amqp-core') {
    dependencies {
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.4.3'
    }
}

project('spring-otp') {
    dependencies {
        compile "org.springframework:spring-beans:$springVersion"
        compile 'org.erlang.otp:jinterface:1.5.3'
    }
}

project('spring-rabbit') {
    dependencies {
        compile project(':spring-amqp-core')
        compile 'com.rabbitmq:amqp-client:1.8.0'
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile "org.springframework:spring-tx:$springVersion"
        testCompile 'org.mockito:mockito-all:1.8.4'
    }
}

project('spring-rabbit-admin') {
    erlangLicense = true
    dependencies {
        compile project(':spring-amqp-core')
        compile project(':spring-otp')
        compile project(':spring-rabbit')
        compile 'com.rabbitmq:amqp-client:1.8.0'
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile "org.springframework:spring-test:$springVersion"
        compile "org.springframework:spring-tx:$springVersion"
        compile 'junit:junit:4.7'
        testCompile 'org.erlang.otp:jinterface:1.5.3'
        testCompile "org.springframework:spring-beans:$springVersion"
        testCompile "org.springframework:spring-core:$springVersion"
    }
}

// -----------------------------------------------------------------------------
// configuration for each individual sample subproject
// see configure(sampleprojects) above for general config
// -----------------------------------------------------------------------------
project(':spring-amqp-samples:helloworld') {
    dependencies {
        compile project(':spring-amqp-core')
        compile project(':spring-rabbit')
        compile project(':spring-rabbit-admin')
        compile 'com.rabbitmq:amqp-client:1.8.0'
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        runtime "cglib:cglib-nodep:2.2"
        runtime "aspectj:aspectjrt:1.5.4"
        runtime "aspectj:aspectjweaver:1.5.4"
        runtime "org.springframework:spring-context-support:$springVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    writePom {
        artifactId = 'helloworld'
        artifactName = 'Spring AMQP Hello World'
        artifactDescription = 'Shows the usage of Spring AMQP integration classes'
    }
}

project(':spring-amqp-samples:stocks') {
    dependencies {
        compile project(':spring-amqp-core')
        compile project(':spring-rabbit')
        compile project(':spring-rabbit-admin')
        compile 'com.rabbitmq:amqp-client:1.8.0'
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "com.jgoodies:forms:1.2.1"
        compile 'org.slf4j:jcl-over-slf4j:1.5.10'
        compile 'org.slf4j:slf4j-api:1.5.10'
        runtime 'org.slf4j:slf4j-log4j12:1.5.10'
        runtime "cglib:cglib-nodep:2.2"
        runtime "aspectj:aspectjrt:1.5.4"
        runtime "aspectj:aspectjweaver:1.5.4"
        runtime "org.springframework:spring-context-support:$springVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }

    // Client and Server @Test cases need rabbitmq-server up
    test.onlyIf { rabbitServerIsRunning() || this.hasProperty('forceServerTests') }

    writePom {
        artifactId = 'stocks'
        artifactName = 'Spring Rabbit Stocks'
        artifactDescription = 'Shows the usage of Spring Rabbit integration classes'
    }
}

def rabbitServerIsRunning() {
    try {
        def port = 5672
        print "is rabbitmq-server running on ${port}: "
        s = new java.net.Socket("localhost", port)
        println "RUNNING"
        return true
    } catch (java.net.ConnectException ex) {
        println "NOT RUNNING"
        println "tests will be skipped as rabbitmq-server is not running"
        println "run with '-PforceServerTests' to force tests to run"
        return false;
    }
}

