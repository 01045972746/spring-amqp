/*
 * Copyright 2002-2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// -----------------------------------------------------------------------------
// Main gradle build file for Spring AMQP
//
// - run `./gradlew(.bat) build` to kick off a complete compile-and-test
//
// - the following imports are from groovy and/or java classes in buildSrc/
//   or from jars on 'buildscript' classpath (in our case, it's all buildSrc)
//   sources in buildSrc are compiled and placed on the classpath automatically
// -----------------------------------------------------------------------------
import org.springframework.build.Version
import org.springframework.build.Version.ReleaseType
import static org.springframework.build.RabbitUtils.rabbitServerIsRunning


// add basic tasks like 'clean' and 'assemble' to the root project. e.g.: allows
// running `gradle clean` from the root project and deleting the build directory
apply plugin: 'base'


// -----------------------------------------------------------------------------
// Configuration for all projects including this one (the root project)
//
// see: settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
allprojects {
    // group will translate to groupId during pom generation and deployment
    group = 'org.springframework.amqp'

    // version will be used in maven pom generation as well as determining
    // where artifacts should be deployed, based on release type of snapshot,
    // milestone or release.  See Version sources in buildSrc/ for more info
    version = new Version('1.0.0.BUILD-SNAPSHOT')

    // defoult set of maven repositories to be used when resolving dependencies
    repositories {
        mavenRepo urls: "file://" + System.properties['user.home'] + "/.m2/repository"
        mavenCentral()
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/milestone'
        mavenRepo urls: 'http://maven.springframework.org/snapshot/'
        mavenRepo urls: 'http://maven.springframework.org/release'
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/release'
        mavenRepo urls: 'http://repository.springsource.com/maven/bundles/external'
    }
}


// -----------------------------------------------------------------------------
// Configuration for all subprojects
// - empty for now, but shown for completeness
//
// see: settings.gradle for list of all subprojects
// -----------------------------------------------------------------------------
subprojects { }


// -----------------------------------------------------------------------------
// Create collections of subprojects - each will receive their own configuration
// - all subprojects within spring-amqp-samples are 'sample projects'
// - all subprojects that start with spring-* are 'java projects'
// - all non-sample java projects are considered 'core projects'
// - documentation-related subprojects are not collected here (there's just one)
//
// see: configure(*) sections below
// -----------------------------------------------------------------------------
sampleparent = project(':spring-amqp-samples')

sampleprojects = sampleparent.subprojects

javaprojects = subprojects.findAll { project ->
    project.path.startsWith(':spring-') && project != sampleparent
}

coreprojects = javaprojects - sampleprojects


// configure the build classpath for resolving the gradle idea plugin.
// this plugin will be built-in in the next gradle release, allowing
// declaration with a simple "apply plugin: 'idea'"
// see: configure(javaprojects) { apply plugin: 'org.gradle.idea' } below
// see: http://jira.codehaus.org/browse/GRADLE-1025
buildscript {
    repositories {
        mavenRepo urls: 'http://gradle.artifactoryonline.com/gradle/plugins'
    }
    dependencies { classpath "org.gradle.plugins:gradle-idea-plugin:0.2" }
}

// -----------------------------------------------------------------------------
// Configuration for all java subprojects
// -----------------------------------------------------------------------------
configure(javaprojects) {
    apply plugin: 'java' // tasks for conventional java lifecycle
    apply plugin: 'maven' // `gradle install` to push jars to local .m2 cache
    apply plugin: 'eclipse' // `gradle eclipse` to generate .classpath/.project
    apply plugin: 'org.gradle.idea' // `gradle idea` to generate .ipr/.iml

    springVersion = '3.0.3.RELEASE'
    rabbitVersion = '1.8.0'
    erlangVersion = '1.5.3'
    slf4jVersion = '1.5.10'

    // dependencies that are common across all java projects
    dependencies {
        testCompile 'junit:junit:4.7'
    }
}


// -----------------------------------------------------------------------------
// Configuration for all core (non-sample) java subprojects
//
// see: individual project(*) configurations below
// -----------------------------------------------------------------------------
configure(coreprojects) {
    // certain projects will need erlang licensing in addition to Apache.
    // false by default, but certain projects will assign true
    erlangLicense = false

    // all core projects should be OSGi-compliant bundles
    // add the bundlor task to ensure proper manifests
    apply from: "$rootDir/gradle/bundlor.gradle"

    // dependencies common to all core projects
    dependencies {
        compile ('log4j:log4j:1.2.15') {
            exclude group: 'javax.mail', name: 'mail'
            exclude group: 'javax.jms', name: 'jms'
            exclude group: 'com.sun.jdmk', name: 'jmxtools'
            exclude group: 'com.sun.jmx', name: 'jmxri'
        }
        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
    }
}


// -----------------------------------------------------------------------------
// Configuration for all sample projects
//
// see: individual project(*) configurations below
// -----------------------------------------------------------------------------
configure(sampleprojects) {
    apply from: "${sampleparent.projectDir}/pom-generation.gradle"

    // samples group will translate to their maven groupId
    group = 'org.springframework.amqp.samples'

    // dependencies common ta all sample projects
    dependencies {
        compile project(':spring-amqp-core')
        compile project(':spring-rabbit')
        compile project(':spring-rabbit-admin')
        compile "com.rabbitmq:amqp-client:$rabbitVersion"
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        runtime 'log4j:log4j:1.2.14'
        runtime 'cglib:cglib-nodep:2.2'
        runtime 'aspectj:aspectjrt:1.5.4'
        runtime 'aspectj:aspectjweaver:1.5.4'
        runtime "org.springframework:spring-context-support:$springVersion"
        testCompile "org.springframework:spring-test:$springVersion"
    }
}


// -----------------------------------------------------------------------------
// Configuration for each individual core java subproject
//
// see: configure(javaprojects) above for general config
// -----------------------------------------------------------------------------
project('spring-amqp-core') {
    dependencies {
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.4.3'
    }
}

project('spring-otp') {
    dependencies {
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.erlang.otp:jinterface:$erlangVersion"
    }
}

project('spring-rabbit') {
    dependencies {
        compile project(':spring-amqp-core')
        compile "com.rabbitmq:amqp-client:$rabbitVersion"
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile "org.springframework:spring-tx:$springVersion"
        testCompile 'org.mockito:mockito-all:1.8.4'
    }
}

project('spring-rabbit-admin') {
    erlangLicense = true

    dependencies {
        compile project(':spring-amqp-core')
        compile project(':spring-otp')
        compile project(':spring-rabbit')
        compile "com.rabbitmq:amqp-client:$rabbitVersion"
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-oxm:$springVersion"
        compile "org.springframework:spring-test:$springVersion"
        compile "org.springframework:spring-tx:$springVersion"
        compile 'junit:junit:4.7'
        testCompile "org.erlang.otp:jinterface:$erlangVersion"
        testCompile "org.springframework:spring-beans:$springVersion"
        testCompile "org.springframework:spring-core:$springVersion"
    }
}


// -----------------------------------------------------------------------------
// Configuration for each individual sample subproject
//
// see: configure(sampleprojects) above for general config
// -----------------------------------------------------------------------------
project(':spring-amqp-samples:helloworld') {
    generatePom {
        artifactId = 'helloworld'
        artifactName = 'Spring AMQP Hello World'
        artifactDescription = 'Shows the usage of Spring AMQP integration classes'
    }
}

project(':spring-amqp-samples:stocks') {
    dependencies {
        compile "org.springframework:spring-beans:$springVersion"
        compile "org.springframework:spring-core:$springVersion"
        compile "org.springframework:spring-context:$springVersion"
        compile "com.jgoodies:forms:1.2.1"
        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
    }

    generatePom {
        artifactId = 'stocks'
        artifactName = 'Spring Rabbit Stocks'
        artifactDescription = 'Shows the usage of Spring Rabbit integration classes'
    }

    // Client and Server @Test cases need a local rabbitmq-server running
    test.onlyIf { rabbitServerIsRunning() || this.hasProperty('forceServerTests') }
}


// -----------------------------------------------------------------------------
// Conditionally import tasks related to releasing and managing the project
// depending on the role played by the current user.
//
// see: gradle.properties for more information on roles
// -----------------------------------------------------------------------------
if (project.properties['role'] == 'buildmaster') {
    // add tasks for releasing the project such as 'dist', 'uploadDist', etc.
    apply from: "$rootDir/gradle/release.gradle"

    // add management tasks like `wrapper` for generating the gradlew* scripts
    apply from: "$rootDir/gradle/wrapper.gradle"

    // if role is 'buildmaster', expose tasks for each core project
    // allowing for deploying artifacts to remote m2 repositories
    configure(coreprojects) {
        apply from: "$rootDir/gradle/maven-deployment.gradle"
    }
}
